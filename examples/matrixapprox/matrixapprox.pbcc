#ifndef MATRIXAPPROX_PBCC
#define MATRIXAPPROX_PBCC

//#ifndef k
//#define k 4
//#endif

#include "../multiply/multiply.pbcc"
#include "../simple/copy.pbcc"
#include "../eigenproblem/EigSym.pbcc"
#include "../simple/transpose.pbcc"
#include "Bisection.pbcc"

main transform MatrixApprox
//param k  //input param: number of singular values used k<=n
from IN[n,n]
through Lambda[2*n], X[2*n,2*n], U[n,n], VT[n,n], H[2*n,2*n], TEMP[n,n]
to OUT[n,n]
accuracy_metric Norm
accuracy_bins 0, 0.3, 0.6, 0.8, 1, 1.5, 2
accuracy_variable k(1,1) //min=1, initial=1
generator matrixapproinputgen
{
	to (H.region(n,0,2*n,n) h) from (IN a) {
		Transpose(h, a);
	}
	to (H.region(0,n,n,2*n) h )from (IN a){
		Copy2D(h, a);
	}
	secondary H.cell(i,j) from() {
		return 0;
	}

	// use optimized hybrid algorithm to find all eigenvalues/eigenvectors
        to (Lambda lam, X x) from (H h){
                EigSym(lam, x, h);
        }

	// find only the k eigenvalues/eigenvectors by Bisection method
	to (Lambda lam, X x) from (H h) {
	        if (k>n)  PetabricksRuntime::abort(); 
		Bisection(lam,x,h,k);
	}

//	Sigma.cell(i) from (Lambda.cell(i) lam) {
//		return -lam;
//	}

//      U.cell(i,j) from (X.cell(i,j+n) x){
//              return -sqrt(2)*x;
//      }

	//combine sigma(k) * U(k)
	U.cell(i,j) from (X.cell(i,j+n) x, Lambda.cell(i) lam){
		return lam*sqrt(2.0)*x;
	}

	//V tranposed
	VT.cell(j,i) from (X.cell(i,j) x){
		return sqrt(2.0)*x;	
	}
		


	to (OUT out, TEMP temp) from (U u, VT vt){
		int i;
		if (k>n)  PetabricksRuntime::abort(); 
		MatrixMultiply(out, u.region(0,0,1,n), vt.region(0,0,n,1) );
		for (i=1; i<k; i++) { 
			//MatrixAdd(out, out, MatrixMultiply(u.region(i,0,i+1,n), vt.region(0,i,n,i+1)) );
			MatrixMultiply(temp, u.region(i,0,i+1,n), vt.region(0,i,n,i+1));
			MatrixAdd(out, out, temp);
		}
	} 
}


transform ComputeError
from A[w,h], B[w,h]
to Error
{
  Error from (A a, B b)
  {
    int i, j;
    ElementT temp, sum = 0;
    for (j = 0; j < h; j++) {
      for (i = 0; i < w; i++) {
        temp = a.cell(i, j) - b.cell(i, j);
        sum += temp * temp;
      }
    }
    return sqrt(sum / (w * h));
  }
}

transform Norm
from OUT[n,n], IN[n,n]
through ZERO[n,n]
to Accuracy
{
	ZERO.cell(i,j) from() { return 0;}

	//accuracy measure
	Accuracy  from(IN in, OUT out, ZERO Z) 
	{
		int i;
		int j;
		ElementT e1;
		ElementT e2;
		ComputeError(e1, Z, in);
		ComputeError(e2, out, in);
		if (e2 == 0) {
            		return 10;
        	}
        	else {
            		return log10(e1/e2);
        	}
	}
			
}

transform matrixapproinputgen
to IN[n,n]
{
  IN.cell(i,j) from() { return PetabricksRuntime::randDouble(0,1); }
}


#endif // MATRIXAPPROX_PBCC
