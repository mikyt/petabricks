#ifndef TRISOLVELL_PBCC
#define TRISOLVELL_PBCC

// Left lower (LL) triangular solve
// Solve AX = B for X, where A is lower triangular

#include "../simple/copy.pbcc"
#include "../simple/scale.pbcc"
#include "../multiply/multiplyAdd.pbcc"
#include "TriSolveLAPACK.pbcc"
#include "TriSolveGenerators.pbcc"

#define MIN(x, y) ((y) < (x) ? (y) : (x))

transform TriSolveLLBase
from UNITFLAG, A[m,m], B[n,m]
to X[n,m]
{
    // right looking (update trailing matrix after each row is solved)
    to   (X x)
    from (UNITFLAG unitFlag, A a, B b)
    {
        Copy2D(x, b);
        for (int i = 0; i < m-1; ++i) {
            if (!unitFlag) {
                Scale(x.region(0, i, n, i+1), 1.0 / a.cell(i, i));
            }
            MatrixMultiplyAdd(x.region(0, i+1, n, m), -1,
                              a.region(i, i+1, i+1, m),
                              x.region(0, i, n, i+1), 1);
        }
        if (!unitFlag) {
            Scale(x.region(0, m-1, n, m), 1.0 / a.cell(m-1, m-1));
        }
    }

    // left looking (process one row at a time)
    to   (X x)
    from (UNITFLAG unitFlag, A a, B b)
    {
        Copy2D(x, b);
        if (!unitFlag) {
            Scale(x.region(0, 0, n, 1), 1.0 / a.cell(0, 0));
        }
        for (int i = 1; i < m; ++i) {
            ElementT factor = unitFlag ? 1 : 1.0 / a.cell(i, i);
            MatrixMultiplyAdd(x.region(0, i, n, i+1), -factor,
                              a.region(0, i, i, i+1),
                              x.region(0, 0, n, i), factor);
        }
    }

    // call LAPACK
    to (X x)
    from (UNITFLAG unitFlag, A a, B b) {
        TriSolveLAPACK(x, 0, 0, unitFlag, 1, a, b);
    }
}

transform TriSolveLLBlocked
from UNITFLAG, A[m,m], B[n,m]
to X[n,m]
tunable blockSize
{
    // right looking (update trailing matrix after each row block is solved)
    to   (X x)
    from (UNITFLAG unitFlag, A a, B b)
    {
        if (blockSize < 1 || blockSize >= m) {
            TriSolveLLBase(x, unitFlag, a, b);
            return;
        }

        MatrixRegion2D x1, x2;
        ConstMatrixRegion2D a11, a21, a22, b1, b2;

        x1 = x.region(0, 0, n, blockSize);
        x2 = x.region(0, blockSize, n, m);

        a11 = a.region(0, 0, blockSize, blockSize);
        a21 = a.region(0, blockSize, blockSize, m);
        a22 = a.region(blockSize, blockSize, m, m);

        b1 = b.region(0, 0, n, blockSize);
        b2 = b.region(0, blockSize, n, m);

        TriSolveLLBase(x1, unitFlag, a11, b1);
        Copy2D(x2, b2);
        MatrixMultiplyAdd(x2, -1, a21, x1, 1);
        TriSolveLLBlocked(x2, unitFlag, a22, x2);
    }

    // left looking (process one row block at a time)
    to   (X x)
    from (UNITFLAG unitFlag, A a, B b)
    {
        if (blockSize < 1 || blockSize >= m) {
            TriSolveLLBase(x, unitFlag, a, b);
            return;
        }

        int i0, i1;
        MatrixRegion2D x1, x2;
        ConstMatrixRegion2D a21, a22, b2;

        for (i0 = 0; i0 < m; i0 += blockSize) {

            i1 = MIN(i0 + blockSize, m);

            x2  = x.region( 0, i0,  n, i1);
            a22 = a.region(i0, i0, i1, i1);
            b2  = b.region( 0, i0,  n, i1);

            if (i0 == 0) {
                TriSolveLLBase(x2, unitFlag, a22, b2);
            } else {

                x1  = x.region(0,  0,  n, i0);
                a21 = a.region(0, i0, i0, i1);

                Copy2D(x2, b2);
                MatrixMultiplyAdd(x2, -1, a21, x1, 1);
                TriSolveLLBase(x2, unitFlag, a22, x2);
            }
        }
    }
}

transform TriSolveLL
from UNITFLAG, A[m,m], B[n,m]
to X[n,m]
generator TriSolveLGenerator
{
    to (X x) from (UNITFLAG unitFlag, A a, B b) {
        TriSolveLLBase(x, unitFlag, a, b);
    }

    to (X x) from (UNITFLAG unitFlag, A a, B b) {
        TriSolveLLBlocked(x, unitFlag, a, b);
    }
}

#endif // TRISOLVELL_PBCC
