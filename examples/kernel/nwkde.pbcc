#ifndef NWKDE_PBCC
#define NWKDE_PBCC

#include "../simple/init.pbcc"
#include "../simple/macros.h"
#include "../simple/reduce.pbcc"
#include "nwkde.h"
#include "nwkdeGenerators.pbcc"
#include "nwkdeMetric.pbcc"
#include "utils.pbcc"

%{

/* wrap relative difference into [-180,180]
 * do most of the arithmetic in integers for speed
 * P360 and M360 indicate "plus 360" and "minus 360" */
inline ElementT wrapWindDirDiff(ElementT diff)
{
    /* add 360 *before* cast to round towards -INF instead of towards 0 */
    int diffIntP360 = (int) (diff + 360);

    /* add 180 to ensure modulo result is positive */
    int diffIntWrapP360 = ((diffIntP360 + 180) % 360) + 180;

    return diff + (diffIntWrapP360 - diffIntP360);
}

%}

transform NWKDECheckInputs
from TRAINDATA[m,n], TRAINX[2,p], TRAINY[2], TRAININDICES[l],
     TESTDATA[m2,n2], TESTX[2,p], TESTINDICES[q]
to INPUTSCHECKED
{
    INPUTSCHECKED
    from (TRAININDICES trainIndices, TRAINX trainX, TRAINY trainY,
          TESTINDICES testIndices, TESTX testX)
    {
        ElementT min, max, min2, max2;

        findMinAndMax(&min, &max, trainX.col(0));
        fprintf(stderr, "trainX.col(0) range: (%g, %g)\n", min, max);
        JASSERT (min >= 0 && max < m)(m).Text("trainX.col(0) out of bounds");

        fprintf(stderr, "trainY.cell(0): %g\n", trainY.cell(0));
        JASSERT (trainY.cell(0) >= 0 && trainY.cell(0) < m)
                (m).Text("trainY.cell(0) out of bounds");

        findMinAndMax(&min, &max, testX.col(0));
        fprintf(stderr, "testX.col(0) range: (%g, %g)\n", min, max);
        JASSERT (min >= 0 && max < m2)(m2).Text("testX.col(0) out of bounds");

        findMinAndMax(&min, &max, trainIndices);
        findMinAndMax(&min2, &max2, trainX.col(1));
        fprintf(stderr, "trainIndices + trainX.col(1) range: (%g, %g)\n",
                min + min2, max + max2);
        JASSERT (min + min2 >= 0 && max + max2 < n)
                (n).Text("trainIndices + trainX.col(1) out of bounds");

        fprintf(stderr, "trainIndices + trainY.cell(1) range: (%g, %g)\n",
                min + trainY.cell(1), max + trainY.cell(1));
        JASSERT (min + trainY.cell(1) >= 0 && max + trainY.cell(1) < n)
                (n).Text("trainIndices + trainY.cell(1) out of bounds");

        findMinAndMax(&min, &max, testIndices);
        findMinAndMax(&min2, &max2, testX.col(1));
        fprintf(stderr, "testIndices + testX.col(1) range: (%g, %g)\n",
               min + min2, max + max2);
        JASSERT (min + min2 >= 0 && max + max2 < n2)
                (n).Text("testIndices + testX.col(1) out of bounds");
    }
}

/*  TRAINDATA - block of data: n time slices, m variables per time slice
    TRAININDICES - l time indices into TRAINDATA to use for training

    TESTDATA - block of data: n2 time slices, m2 variables per time slice
    TESTINDICES - q indices into TESTDATA to evaluate the regression

    For each time index in TRAININDICES or TESTINDICES, we associate
    a p-dim vector of predictors for use during regression.  Each predictor
    variable is represented by a 2-element row in TRAINX.  The first element
    of each row is the column index (into TRAINDATA) of the variable.  The
    second element is the time offset relative to the current time index.

    In this way, we can build predictor vectors that contain overlapping data
    for different time indices.  TRAINY specifies the output variable location
    in TRAINDATA for each time index.  Finally, TESTX specifies how to
    construct the predictor vector in TESTDATA.

    DIRFLAGS - indicates whether TRAINDATA column corresponds to a wind
               direction \in [0, 360]
    SPLIT - set to 0 for normal operation.  If non-zero, determines how many
            partitions to split the training set to do n-fold cross validation
    KERNELWIDTHS - width of the kernel function to use for a data column
    MASKWIDTH - width of training mask.  This tells the transform to omit
                training indices less than MASKWIDTH indices of the test index
                when computing the regression.  Note: to be used when TESTDATA
                is equal to TRAINDATA.  May set to 0 (no mask effect) if
                TESTDATA is separate from TRAINDATA.

    SQDIFFS - squared differences for each predictor for each train-test
              point pair
    WEIGHTS - weights computed with Gaussian kernel function for each
              train-test point pair
    PARTIALS - weighted output partial sums
*/

// Compute results directly with one pass through the data
transform NWKDEDirect
from TRAINDATA[m,n], TRAINX[2,p], TRAINY[2], TRAININDICES[l], TRAINSTART,
     TESTDATA[m2,n2], TESTX[2,p], TESTINDICES[q], TESTSTART, NUMTEST,
     DIRFLAGS[m], KERNELWIDTHS[m], SPLIT, MASKWIDTH
to RESULTS[q]
{
    to   (RESULTS.cell(k) result)
    from (TRAINDATA trainData, TRAINX trainX, TRAINY trainY,
          TRAININDICES trainIndices, TRAINSTART trainStart,
          TESTDATA testData, TESTX testX,
          TESTINDICES.cell(k) testIndex, TESTSTART testStart, NUMTEST numTest,
          DIRFLAGS dirFlags, KERNELWIDTHS kernelWidths, SPLIT split,
          MASKWIDTH maskWidth)
    {
        bool skipIndexFlag;
        IndexT i, j, idx, dirFlag, trainIndex, partSize, partStart, partEnd;
        ElementT y, kernelWidth, trainPoint, testPoint, diff, sum,
                 curWeight, curPartial, weight = 0, partial = 0;

        // compute the partition that this testIndex lies in
        partStart = partEnd = idx = testStart + k;
        if (split > 0) {
            partSize = MAX(1, floor(numTest / split));
            partStart = idx - (idx % partSize);
            partEnd = partStart + partSize;
        }

        // loop over training points
        for (j = 0; j < l; ++j) {

            trainIndex = trainIndices.cell(j);
            y = trainData.cell(trainY.cell(0), trainIndex + trainY.cell(1));

            // skip current training index if it is too close to the test
            // index, if it's not in the same partition, or if the training
            // output value is a NAN
            if ((trainIndex > testIndex - maskWidth &&
                 trainIndex < testIndex + maskWidth) ||
                (trainStart + j >= partStart &&
                 trainStart + j < partEnd) ||
                ISNAN(y)) {
                continue;
            }

            // loop over predictor variables to calculate training weight
            sum = 0;
            skipIndexFlag = false;
            for (i = 0; i < trainX.size(1); ++i) {
                dirFlag     =     dirFlags.cell(trainX.cell(0, i));
                kernelWidth = kernelWidths.cell(trainX.cell(0, i));
                trainPoint = trainData.cell(trainX.cell(0, i),
                                            trainIndex + trainX.cell(1, i));
                testPoint = testData.cell(testX.cell(0, i),
                                          testIndex + testX.cell(1, i));

                // skip this variable if nan is detected in the test point
                if (ISNAN(testPoint)) { continue; }

                // skip this training index if nan is detected
                if (ISNAN(trainPoint)) { skipIndexFlag = true; break; }

                // compute diff (and wrap if dirFlag is true)
                diff = trainPoint - testPoint;
                if (dirFlag) { diff = wrapWindDirDiff(diff); }

                // normalize and save squared difference
                diff /= kernelWidth;
                sum += diff * diff;
            }

            if (skipIndexFlag) { continue; }

            curWeight = exp(-((ElementT) sum));
            curPartial = curWeight * y;

            weight += curWeight;
            partial += curPartial;
        }

        result = partial / weight;
    }
}

#define INCLUDE_METHOD2
#define INCLUDE_METHOD3

// The three methods below are listed in decreasing order of both available
// parallelism and memory traffic.
transform NWKDEBase
from TRAINDATA[m,n], TRAINX[2,p], TRAINY[2], TRAININDICES[l], TRAINSTART,
     TESTDATA[m2,n2], TESTX[2,p], TESTINDICES[q], TESTSTART, NUMTEST,
     DIRFLAGS[m], KERNELWIDTHS[m], SPLIT, MASKWIDTH
to WEIGHTS[q], PARTIALS[q]
{

#ifdef INCLUDE_METHOD1
    to   (WEIGHTS weights, PARTIALS partials)
    from (TRAINDATA trainData, TRAINX trainX, TRAINY trainY,
          TRAININDICES trainIndices, TRAINSTART trainStart,
          TESTDATA testData, TESTX testX,
          TESTINDICES testIndices, TESTSTART testStart, NUMTEST numTest,
          DIRFLAGS dirFlags, KERNELWIDTHS kernelWidths, SPLIT split,
          MASKWIDTH maskWidth)
    {
        NWKDEBase1(weights, partials,
                   trainData, trainX, trainY, trainIndices, trainStart,
                   testData, testX, testIndices, testStart, numTest,
                   dirFlags, kernelWidths, split, maskWidth);
    }
#endif

#ifdef INCLUDE_METHOD2
    to   (WEIGHTS weights, PARTIALS partials)
    from (TRAINDATA trainData, TRAINX trainX, TRAINY trainY,
          TRAININDICES trainIndices, TRAINSTART trainStart,
          TESTDATA testData, TESTX testX,
          TESTINDICES testIndices, TESTSTART testStart, NUMTEST numTest,
          DIRFLAGS dirFlags, KERNELWIDTHS kernelWidths, SPLIT split,
          MASKWIDTH maskWidth)
    {
        NWKDEBase2(weights, partials,
                   trainData, trainX, trainY, trainIndices, trainStart,
                   testData, testX, testIndices, testStart, numTest,
                   dirFlags, kernelWidths, split, maskWidth);
    }
#endif

#ifdef INCLUDE_METHOD3
    to   (WEIGHTS weights, PARTIALS partials)
    from (TRAINDATA trainData, TRAINX trainX, TRAINY trainY,
          TRAININDICES trainIndices, TRAINSTART trainStart,
          TESTDATA testData, TESTX testX,
          TESTINDICES testIndices, TESTSTART testStart, NUMTEST numTest,
          DIRFLAGS dirFlags, KERNELWIDTHS kernelWidths, SPLIT split,
          MASKWIDTH maskWidth)
    {
        NWKDEBase3(weights, partials,
                   trainData, trainX, trainY, trainIndices, trainStart,
                   testData, testX, testIndices, testStart, numTest,
                   dirFlags, kernelWidths, split, maskWidth);
    }
#endif
}

// Compute weights and partials directly
transform NWKDEBase2
from TRAINDATA[m,n], TRAINX[2,p], TRAINY[2], TRAININDICES[l], TRAINSTART,
     TESTDATA[m2,n2], TESTX[2,p], TESTINDICES[q], TESTSTART, NUMTEST,
     DIRFLAGS[m], KERNELWIDTHS[m], SPLIT, MASKWIDTH
to WEIGHTS[q], PARTIALS[q]
through WEIGHTS2[l,q], PARTIALS2[l,q]
{
    to   (PARTIALS2.cell(j,k) partial, WEIGHTS2.cell(j,k) weight)
    from (TRAINDATA trainData, TRAINX trainX, TRAINY trainY,
          TRAININDICES.cell(j) trainIndex, TRAINSTART trainStart,
          TESTDATA testData, TESTX testX,
          TESTINDICES.cell(k) testIndex, TESTSTART testStart, NUMTEST numTest,
          DIRFLAGS dirFlags, KERNELWIDTHS kernelWidths, SPLIT split,
          MASKWIDTH maskWidth)
    {
        IndexT i, idx, dirFlag, partStart, partEnd, partSize;
        ElementT y, kernelWidth, trainPoint, testPoint, diff, sum;

        // compute the partition that this testIndex lies in
        partStart = partEnd = idx = testStart + k;
        if (split > 0) {
            partSize = MAX(1, floor(numTest / split));
            partStart = idx - (idx % partSize);
            partEnd = partStart + partSize;
        }

        y = trainData.cell(trainY.cell(0), trainIndex + trainY.cell(1));

        // skip current training index if it is too close to the test
        // index, if it's not in the same partition, or if the training
        // output value is a NAN
        if ((trainIndex > testIndex - maskWidth &&
             trainIndex < testIndex + maskWidth) ||
            (trainStart + j >= partStart &&
             trainStart + j < partEnd) ||
            ISNAN(y)) {
            weight = partial = 0;
            return;
        }

        // loop over predictor variables to calculate training weight
        sum = 0;
        for (i = 0; i < trainX.size(1); ++i) {
            dirFlag     =     dirFlags.cell(trainX.cell(0, i));
            kernelWidth = kernelWidths.cell(trainX.cell(0, i));
            trainPoint = trainData.cell(trainX.cell(0, i),
                                        trainIndex + trainX.cell(1, i));
            testPoint = testData.cell(testX.cell(0, i),
                                      testIndex + testX.cell(1, i));

            // skip this variable if nan is detected in the test point
            if (ISNAN(testPoint)) { continue; }

            // skip this training index if nan is detected
            if (ISNAN(trainPoint)) { weight = partial = 0; return; }

            // compute diff (and wrap if dirFlag is true)
            diff = trainPoint - testPoint;
            if (dirFlag) { diff = wrapWindDirDiff(diff); }

            // normalize and save squared difference
            diff /= kernelWidth;
            sum += diff * diff;
        }

        weight = exp(-((ElementT) sum));
        partial = weight * y;
    }

    to (PARTIALS.cell(k) partial,
        WEIGHTS.cell(k) weight)
    from (PARTIALS2.row(k) partials,
          WEIGHTS2.row(k) weights)
    {
        ElementT curPartial, curWeight;
        ReduceAdd1D(curPartial, partials);
        ReduceAdd1D(curWeight, weights);
        partial += curPartial;
        weight += curWeight;
    }
}

// Compute results directly with one pass through the data
transform NWKDEBase3
from TRAINDATA[m,n], TRAINX[2,p], TRAINY[2], TRAININDICES[l], TRAINSTART,
     TESTDATA[m2,n2], TESTX[2,p], TESTINDICES[q], TESTSTART, NUMTEST,
     DIRFLAGS[m], KERNELWIDTHS[m], SPLIT, MASKWIDTH
to WEIGHTS[q], PARTIALS[q]
{
    to   (WEIGHTS.cell(k) weight, PARTIALS.cell(k) partial)
    from (TRAINDATA trainData, TRAINX trainX, TRAINY trainY,
          TRAININDICES trainIndices, TRAINSTART trainStart,
          TESTDATA testData, TESTX testX,
          TESTINDICES.cell(k) testIndex, TESTSTART testStart, NUMTEST numTest,
          DIRFLAGS dirFlags, KERNELWIDTHS kernelWidths, SPLIT split,
          MASKWIDTH maskWidth)
    {
        bool skipIndexFlag;
        IndexT i, j, idx, dirFlag, trainIndex, partSize, partStart, partEnd;
        ElementT y, kernelWidth, trainPoint, testPoint, diff, sum,
                 curWeight, curPartial;

        // compute the partition that this testIndex lies in
        partStart = partEnd = idx = testStart + k;
        if (split > 0) {
            partSize = MAX(1, floor(numTest / split));
            partStart = idx - (idx % partSize);
            partEnd = partStart + partSize;
        }

        // loop over training points
        for (j = 0; j < l; ++j) {

            trainIndex = trainIndices.cell(j);
            y = trainData.cell(trainY.cell(0), trainIndex + trainY.cell(1));

            // skip current training index if it is too close to the test
            // index, if it's not in the same partition, or if the training
            // output value is a NAN
            if ((trainIndex > testIndex - maskWidth &&
                 trainIndex < testIndex + maskWidth) ||
                (trainStart + j >= partStart &&
                 trainStart + j < partEnd) ||
                ISNAN(y)) {
                continue;
            }

            // loop over predictor variables to calculate training weight
            sum = 0;
            skipIndexFlag = false;
            for (i = 0; i < trainX.size(1); ++i) {
                dirFlag     =     dirFlags.cell(trainX.cell(0, i));
                kernelWidth = kernelWidths.cell(trainX.cell(0, i));
                trainPoint = trainData.cell(trainX.cell(0, i),
                                            trainIndex + trainX.cell(1, i));
                testPoint = testData.cell(testX.cell(0, i),
                                          testIndex + testX.cell(1, i));

                // skip this variable if nan is detected in the test point
                if (ISNAN(testPoint)) { continue; }

                // skip this training index if nan is detected
                if (ISNAN(trainPoint)) { skipIndexFlag = true; break; }

                // compute diff (and wrap if dirFlag is true)
                diff = trainPoint - testPoint;
                if (dirFlag) { diff = wrapWindDirDiff(diff); }

                // normalize and save squared difference
                diff /= kernelWidth;
                sum += diff * diff;
            }

            if (skipIndexFlag) { continue; }

            curWeight = exp(-((ElementT) sum));
            curPartial = curWeight * y;

            weight += curWeight;
            partial += curPartial;
        }
    }
}

// Compute results in a blocked fashion
transform NWKDEBlocked
from TRAINDATA[m,n], TRAINX[2,p], TRAINY[2], TRAININDICES[l], TRAINSTART,
     TESTDATA[m2,n2], TESTX[2,p], TESTINDICES[q], TESTSTART, NUMTEST,
     DIRFLAGS[m], KERNELWIDTHS[m], SPLIT, MASKWIDTH
to WEIGHTS[q], PARTIALS[q]
tunable sizespecific blockSize
{
    to   (WEIGHTS weights, PARTIALS partials)
    from (TRAINDATA trainData, TRAINX trainX, TRAINY trainY,
          TRAININDICES trainIndices, TRAINSTART trainStart,
          TESTDATA testData, TESTX testX,
          TESTINDICES testIndices, TESTSTART testStart, NUMTEST numTest,
          DIRFLAGS dirFlags, KERNELWIDTHS kernelWidths, SPLIT split,
          MASKWIDTH maskWidth)
    {
        IndexT i0, i1, j0, j1, j, b = MAX(1, blockSize);

        // blocked iteration over training and test indices
        for (j0 = 0; j0 < q; j0 += b) {
            j1 = MIN(j0 + b, q);
            for (i0 = 0; i0 < l; i0 += b) {
                i1 = MIN(i0 + b, l);
                NWKDEBase(weights.region(j0, j1), partials.region(j0, j1),
                          trainData, trainX, trainY,
                          trainIndices.region(i0, i1), trainStart + i0,
                          testData, testX,
                          testIndices.region(j0, j1), testStart + j0, numTest,
                          dirFlags, kernelWidths, split, maskWidth);
            }
        }
    }
}

// Cache-oblivous recursive version, split iteration space into quadrants and
// recurse in order to minimize cache traffic
transform NWKDERecursive
from TRAINDATA[m,n], TRAINX[2,p], TRAINY[2], TRAININDICES[l], TRAINSTART,
     TESTDATA[m2,n2], TESTX[2,p], TESTINDICES[q], TESTSTART, NUMTEST,
     DIRFLAGS[m], KERNELWIDTHS[m], SPLIT, MASKWIDTH
to WEIGHTS[q], PARTIALS[q]
tunable sizespecific blockSize
{
    to   (WEIGHTS weights, PARTIALS partials)
    from (TRAINDATA trainData, TRAINX trainX, TRAINY trainY,
          TRAININDICES trainIndices, TRAINSTART trainStart,
          TESTDATA testData, TESTX testX,
          TESTINDICES testIndices, TESTSTART testStart, NUMTEST numTest,
          DIRFLAGS dirFlags, KERNELWIDTHS kernelWidths, SPLIT split,
          MASKWIDTH maskWidth)
    {
        IndexT i0, i1, j0, j1, ib, jb, b = MAX(1, blockSize);

        // base case
        if (l <= b && q <= b) {
            NWKDEBlocked(weights, partials,
                         trainData, trainX, trainY,
                         trainIndices, trainStart,
                         testData, testX,
                         testIndices, testStart, numTest,
                         dirFlags, kernelWidths, split, maskWidth);
            return;
        }

        // determine block sizes in each dimension
        ib = (l > b) ? l/2 : l;
        jb = (q > b) ? q/2 : q;

        // quadrant 1
        i0 = 0; i1 = ib;
        j0 = 0; j1 = jb;
        NWKDERecursive(weights.region(j0, j1), partials.region(j0, j1),
                       trainData, trainX, trainY,
                       trainIndices.region(i0, i1), trainStart + i0,
                       testData, testX,
                       testIndices.region(j0, j1), testStart + j0, numTest,
                       dirFlags, kernelWidths, split, maskWidth);

        // quadrant 2
        if (ib < l) {
            i0 = ib; i1 = l;
            j0 = 0; j1 = jb;
            NWKDERecursive(weights.region(j0, j1), partials.region(j0, j1),
                           trainData, trainX, trainY,
                           trainIndices.region(i0, i1), trainStart + i0,
                           testData, testX,
                           testIndices.region(j0, j1), testStart + j0, numTest,
                           dirFlags, kernelWidths, split, maskWidth);
        }

        if (jb < q) {
            // quadrant 4
            if (ib < l) {
                i0 = ib; i1 = l;
                j0 = jb; j1 = q;
                NWKDERecursive(weights.region(j0, j1), partials.region(j0, j1),
                               trainData, trainX, trainY,
                               trainIndices.region(i0, i1), trainStart + i0,
                               testData, testX, testIndices.region(j0, j1),
                               testStart + j0, numTest,
                               dirFlags, kernelWidths, split, maskWidth);
            }

            // quadrant 3
            i0 = 0; i1 = ib;
            j0 = jb; j1 = q;
            NWKDERecursive(weights.region(j0, j1), partials.region(j0, j1),
                           trainData, trainX, trainY,
                           trainIndices.region(i0, i1), trainStart + i0,
                           testData, testX,
                           testIndices.region(j0, j1), testStart + j0, numTest,
                           dirFlags, kernelWidths, split, maskWidth);
        }
    }
}

transform NWKDE
from TRAINDATA[_M_,_N_], TRAINX[2,_P_], TRAINY[2], TRAININDICES[_L_],
     TESTDATA[_M2_,_N2_], TESTX[2,_P_], TESTINDICES[_Q_],
     DIRFLAGS[_M_], KERNELWIDTHS[_M_], SPLIT, MASKWIDTH
to RESULT[_Q_]
through WEIGHTS[_Q_], PARTIALS[_Q_]
tunable sizespecific blockSize
through INPUTSCHECKED
generator NWKDEGenerator2
accuracy_metric NWKDEMetric2
{
    to   (RESULT result)
    from (TRAINDATA trainData, TRAINX trainX,
          TRAINY trainY, TRAININDICES trainIndices,
          TESTDATA testData, TESTX testX, TESTINDICES testIndices,
          DIRFLAGS dirFlags, KERNELWIDTHS kernelWidths, SPLIT split,
          MASKWIDTH maskWidth)
    {
        NWKDEDirect(result,
                    trainData, trainX, trainY, trainIndices, 0,
                    testData, testX, testIndices, 0, _Q_,
                    dirFlags, kernelWidths, split, maskWidth);
    }

    to   (WEIGHTS weights, PARTIALS partials)
    from (TRAINDATA trainData, TRAINX trainX,
          TRAINY trainY, TRAININDICES trainIndices,
          TESTDATA testData, TESTX testX, TESTINDICES testIndices,
          DIRFLAGS dirFlags, KERNELWIDTHS kernelWidths, SPLIT split,
          MASKWIDTH maskWidth)
    {
#ifdef DEBUG
        ElementT ret;
        NWKDECheckInputs(ret, trainData, trainX, trainY, trainIndices,
                         testData, testX, testIndices);
#endif

        // initialize weights and partials to 0
        Init1D(weights, 0);
        Init1D(partials, 0);

        if (blockSize < 1 || blockSize > _Q_) {
            NWKDERecursive(weights, partials,
                           trainData, trainX, trainY, trainIndices, 0,
                           testData, testX, testIndices, 0, _Q_,
                           dirFlags, kernelWidths, split, maskWidth);
        } else {
            // domain decomposition for coarse-grain parallelism
            IndexT i0, i1;
            for (i0 = 0; i0 < _Q_; i0 += blockSize) {
                i1 = MIN(i0 + blockSize, _Q_);
                spawn NWKDERecursive(weights.region(i0, i1),
                                     partials.region(i0, i1),
                                     trainData, trainX, trainY, trainIndices, 0,
                                     testData, testX,
                                     testIndices.region(i0, i1), i0, _Q_,
                                     dirFlags, kernelWidths, split, maskWidth);
            }
            sync;
        }
    }

    to (RESULT.cell(j) result)
    from (WEIGHTS.cell(j) weight, PARTIALS.cell(j) partial)
    {
        result = partial / weight;
    }
}

#endif // NWKDE_PBCC
